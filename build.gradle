plugins {
    id 'java'
    id 'idea'
    id 'groovy'
    id 'signing'
    id 'java-library'
    id 'maven-publish'
    id 'org.springframework.boot' version '2.7.0'
    id 'io.spring.dependency-management' version '1.0.14.RELEASE'
}

group = 'com.github.joutvhu'
version = '2.7.5'
sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

ext {
    versions = [
            'spring-boot.version'           : '2.7.0',
            'spring-framework.version'      : '5.3.20',
            'jakarta-transaction.version'   : '1.3.3',
            'jakarta-persistence.version'   : '2.2.3',
            'hibernate.version'             : '5.6.9.Final',
            'freemarker.version'            : '2.3.31',
            'commons-lang3.version'         : '3.12.0'
    ]
    managedVersions = [
            'org.springframework:spring-aspects'          : 'spring-framework.version',
            'org.springframework:spring-context'          : 'spring-framework.version',
            'org.springframework:spring-jcl'              : 'spring-framework.version',
            'org.springframework:spring-context-support'  : 'spring-framework.version',
            'org.springframework.data:spring-data-commons': 'spring-boot.version',
            'org.springframework.data:spring-data-jpa'    : 'spring-boot.version',
            'jakarta.transaction:jakarta.transaction-api' : 'jakarta-transaction.version',
            'jakarta.persistence:jakarta.persistence-api' : 'jakarta-persistence.version',
            'org.hibernate:hibernate-core'                : 'hibernate.version',
            'org.freemarker:freemarker'                   : 'freemarker.version',
            'org.apache.commons:commons-lang3'            : 'commons-lang3.version'
    ]
    otherVersions = [
            'org.springframework.boot:spring-boot-dependencies': 'spring-boot.version'
    ]
}

dependencyManagement {
    dependencies {
        ext.managedVersions.each {
            dependency "${it.key}:${ext.versions[it.value]}"
        }
    }
}

dependencies {
    implementation 'org.springframework:spring-aspects'
    implementation 'org.springframework:spring-context'
    implementation 'org.springframework:spring-context-support'

    implementation 'org.springframework.data:spring-data-jpa'
    implementation 'org.springframework.data:spring-data-commons'

    implementation 'jakarta.transaction:jakarta.transaction-api'
    implementation 'jakarta.persistence:jakarta.persistence-api'

    implementation 'org.freemarker:freemarker'
    implementation 'org.apache.commons:commons-lang3'

    implementation('org.hibernate:hibernate-core') {
        exclude group: 'javax.activation', module: 'javax.activation-api'
        exclude group: 'javax.persistence', module: 'javax.persistence-api'
        exclude group: 'javax.xml.bind', module: 'jaxb-api'
        exclude group: 'org.jboss.spec.javax.transaction', module: 'jboss-transaction-api_1.2_spec'
    }

    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    testImplementation(platform('org.junit:junit-bom:5.8.2'))
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation('org.springframework.boot:spring-boot-starter-test:2.7.0') {
        exclude group: 'junit', module: 'junit'
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-api'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
    }
    testImplementation 'com.h2database:h2'

    testCompileOnly 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'
}

jar {
    enabled = true
    manifest {
        attributes 'Built-By': 'joutvhu (Giao Ho)'
    }
    into("META-INF/maven/${project.group}/${project.name}") {
        from {
            generatePomFileForMavenPublication
        }
        rename {
            it.replace('pom-default.xml', 'pom.xml')
        }
    }
}

bootJar {
    enabled = false
}

task fatJar(type: Jar) {
    manifest.from jar.manifest
    with jar
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives fatJar, sourcesJar, javadocJar
}

test {
    useJUnitPlatform()
    filter {
        includeTestsMatching 'com.joutvhu.dynamic.jpa.*'
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = project.name
            artifacts = [fatJar, sourcesJar, javadocJar]
            version = version

            pom {
                name = project.name
                description = 'Easier to implement dynamic queries with JpaRepository'
                url = 'https://github.com/joutvhu/spring-dynamic-jpa'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/joutvhu/spring-dynamic-jpa/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'joutvhu'
                        name = 'Giao Ho'
                        email = 'joutvhu@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:joutvhu/spring-dynamic-jpa.git'
                    developerConnection = 'scm:git:git@github.com:joutvhu/spring-dynamic-jpa.git'
                    url = 'https://github.com/joutvhu/spring-dynamic-jpa'
                }
                issueManagement {
                    system = 'Github Issue'
                    url = 'https://github.com/joutvhu/spring-dynamic-jpa/issues'
                }
                organization {
                    name = 'Giao Ho'
                    url = 'https://github.com/joutvhu'
                }
                properties = project.ext.versions
                withXml {
                    def dependencyManagement = asNode().get('dependencyManagement')
                    dependencyManagement.dependencies.dependency.each {
                        def key = "${it.groupId.text()}:${it.artifactId.text()}"
                        def versionProperty = project.ext.managedVersions[key]
                        if (versionProperty == null) {
                            versionProperty = project.ext.otherVersions[key]
                        }
                        if (versionProperty) {
                            it.version[0].setValue("\${$versionProperty}")
                        }
                    }

                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = 'sonatype'
            if (version.endsWith('-SNAPSHOT') || version.endsWith('.SNAPSHOT')) {
                url = 'https://oss.sonatype.org/content/repositories/snapshots'
            } else {
                url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            }
            credentials {
                username = project.ossrhUsername
                password = project.ossrhPassword
            }
        }
        maven {
            name = 'github'
            url = "https://maven.pkg.github.com/joutvhu/spring-dynamic-jpa"
            credentials {
                username = project.githubUsername
                password = project.githubPassword
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}
