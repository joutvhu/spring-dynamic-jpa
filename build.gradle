plugins {
    id 'java'
    id 'idea'
    id 'groovy'
    id 'signing'
    id 'java-library'
    id 'maven-publish'
    id 'org.springframework.boot' version '2.1.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
}

group = 'com.github.joutvhu'
version = '2.1.4'
sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'org.springframework:spring-aspects:5.1.2.RELEASE'
    implementation 'org.springframework:spring-context:5.1.2.RELEASE'
    implementation 'org.springframework:spring-context-support:5.1.2.RELEASE'

    implementation 'org.springframework.data:spring-data-jpa:2.1.0.RELEASE'
    implementation 'org.springframework.data:spring-data-commons:2.1.0.RELEASE'

    implementation 'javax.persistence:javax.persistence-api:2.2'
    implementation 'javax.transaction:javax.transaction-api:1.3'

    implementation('org.hibernate:hibernate-core:5.3.7.Final') {
        exclude group: 'javax.activation', module: 'javax.activation-api'
        exclude group: 'javax.persistence', module: 'javax.persistence-api'
        exclude group: 'javax.xml.bind', module: 'jaxb-api'
        exclude group: 'org.jboss.spec.javax.transaction', module: 'jboss-transaction-api_1.2_spec'
    }

    annotationProcessor 'org.projectlombok:lombok:1.18.12'
    implementation 'org.projectlombok:lombok:1.18.12'

    implementation 'org.freemarker:freemarker:2.3.30'
    implementation 'org.apache.commons:commons-lang3:3.11'
    implementation 'org.apache.logging.log4j:log4j-api:2.17.0'

    testImplementation(platform('org.junit:junit-bom:5.8.2'))
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'junit', module: 'junit'
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-api'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
    }
    testImplementation 'org.apache.logging.log4j:log4j-to-slf4j:2.17.0'
    testImplementation 'com.h2database:h2'
}

jar {
    enabled = true
    manifest {
        attributes 'Built-By': 'joutvhu (Giao Ho)'
    }
    into("META-INF/maven/${project.group}/${project.name}") {
        from {
            generatePomFileForMavenPublication
        }
        rename {
            it.replace('pom-default.xml', 'pom.xml')
        }
    }
}

bootJar {
    enabled = false
}

task fatJar(type: Jar) {
    manifest.from jar.manifest
    with jar
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives fatJar, sourcesJar, javadocJar
}

test {
    useJUnitPlatform()
    filter {
        includeTestsMatching 'com.joutvhu.dynamic.jpa.*'
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = project.name
            artifacts = [fatJar, sourcesJar, javadocJar]
            version = version

            pom {
                name = project.name
                description = 'Easier to implement dynamic queries with JpaRepository'
                url = 'https://github.com/joutvhu/spring-dynamic-jpa'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/joutvhu/spring-dynamic-jpa/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'joutvhu'
                        name = 'Giao Ho'
                        email = 'joutvhu@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:joutvhu/spring-dynamic-jpa.git'
                    developerConnection = 'scm:git:git@github.com:joutvhu/spring-dynamic-jpa.git'
                    url = 'https://github.com/joutvhu/spring-dynamic-jpa'
                }
                issueManagement {
                    system = 'Github Issue'
                    url = 'https://github.com/joutvhu/spring-dynamic-jpa/issues'
                }
                organization {
                    name = 'Giao Ho'
                    url = 'https://github.com/joutvhu'
                }
                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = 'sonatype'
            if (version.endsWith('-SNAPSHOT') || version.endsWith('.SNAPSHOT')) {
                url = 'https://oss.sonatype.org/content/repositories/snapshots'
            } else {
                url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            }
            credentials {
                username = project.ossrhUsername
                password = project.ossrhPassword
            }
        }
        maven {
            name = 'github'
            url = "https://maven.pkg.github.com/joutvhu/spring-dynamic-jpa"
            credentials {
                username = project.githubUsername
                password = project.githubPassword
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}
